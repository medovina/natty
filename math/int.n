using nat;

# Integers: definition

Axiom.  There exists a type ℤ (the integers) with a function z : ℕ ⨯ ℕ → ℤ such that

  a. For all a, b, c, d ∈ ℕ, z(a, b) = z(c, d) if and only if a + d = b + c.  [equality on ℤ]
  b. For all x : ℤ, there exist some a, b : ℕ such that x = z(a, b).

Definition.  Let 0 : ℤ = z(0, 0).  Let 1 : ℤ = z(1, 0).

# Addition

Axiom.  There is a binary operation + : ℤ → ℤ → ℤ such that for all n, j, k, i : ℕ,

    z(n, j) + z(k, i) = z(n + k, j + i).

Axiom.  There is a unary operation − : ℤ → ℤ such that for all a, b ∈ ℕ,

  −z(a, b) = z(b, a).

Theorem.  Let a, b, c : ℤ.

  a. a + b = b + a.
  b. a + (b + c) = (a + b) + c.
  c. a + 0 = a.
  d. a + (−a) = 0.

Proof.

  a. There are some n, j, k, i : ℕ such that a = z(n, j) and b = z(k, i).  Then

    a + b = z(n, j) + z(k, i)
          = z(n + k, j + i)
          = z(k + n, i + j)
          = z(k, i) + z(n, j)
          = b + a.

  b. There are some n, j, k, i, m, h ∈ ℕ such that a = z(n, j) and b = z(k, i) and c = z(m, h).  Then
  
    (a + b) + c = (z(n, j) + z(k, i)) + z(m, h)
                    = z(n + k, j + i) + z(m, h)
                    = z((n + k) + m, (j + i) + h)
                    = z(n + (k + m), j + (i + h))
                    = z(n, j) + z(k + m, i + h)
                    = z(n, j) + (z(k, i) + z(m, h))
                    = a + (b + c).

  c. There are some n, j : ℕ such that a = z(n, j).  So

    a + 0 = z(n, j) + z(0, 0)
            = z(n + 0, j + 0)
            = z(n, j)
            = a.

  d. There are some n, j : ℕ such that a = z(n, j).  So

    a + (−a) = z(n, j) + −z(n, j)
                = z(n, j) + z(j, n)
                = z(n + j, j + n)
                = z(0, 0)     by [equality on ℤ]
                = 0.

# Integers: multiplication

Axiom.  There is a binary operation · : ℤ → ℤ → ℤ such that for all n, j, k, i : ℕ,

    z(n, j) · z(k, i) = z(n · k + j · i, j · k + n · i).  [def of ·]

Theorem.  Let a, b, c : ℤ.

  a. a · b = b · a.
  b. (a · b) · c = a · (b · c).
  c. a · (b + c) = a · b + a · c.
  d. a · 1 = a.

Proof.

  a. There are some n, j, k, i : ℕ such that a = z(n, j) and b = z(k, i).  Then

    a · b = z(n, j) · z(k, i)
            = z(n · k + j · i, j · k + n · i)
            = z(k · n + i · j, i · n + k · j)
            = z(k, i) · z(n, j)
            = b · a.

  b. There are some n, j, k, i, m, h : ℕ such that a = z(n, j) and b = z(k, i) and c = z(m, h).  Then

    (a · b) · c = (z(n, j) · z(k, i)) · z(m, h)
          = z(n · k + j · i, j · k + n · i) · z(m, h)
          = z((n · k + j · i) · m + (j · k + n · i) · h,
                 (j · k + n · i) · m + (n · k + j · i) · h)
          = z(((n · k) · m + (j · i) · m) + ((j · k) · h + (n · i) · h),
                 ((j · k) · m + (n · i) · m) + ((n · k) · h + (j · i) · h)) by [right distributive]
          = z(n · (k · m) + n · (i · h) + j · (i · m) + j · (k · h),
                 j · (k · m) + j · (i · h) + n · (i · m) + n · (k · h))
                  by [associativity of multiplication] and [commutativity of addition]
          = z(n · (k · m + i · h) + j · (i · m + k · h),
                 j · (k · m + i · h) + n · (i · m + k · h))  by [right distributive]
          = z(n, j) · z(k · m + i · h, i · m + k · h)
          = z(n, j) · (z(k, i) · z(m, h))
          = a · (b · c).

  c. There are some n, j, k, i, m, h : ℕ such that a = z(n, j) and b = z(k, i) and c = z(m, h).  Then

      a · (b + c) = z(n, j) · (z(k, i) + z(m, h))
                      = z(n, j) · z(k + m, i + h)
                      = z(n · (k + m) + j · (i + h), j · (k + m) + n · (i + h))
                      = z(n · k + n · m + j · i + j · h,
                          j · k + j · m + n · i + n · h)
                      = z(n · k + j · i,  j · k + n · i) +
                            z(n · m + j · h, j · m + n · h)
                      = z(n, j) · z(k, i) + z(n, j) · z(m, h) by [def of ·]
                      = a · b + a · c.

  d. There are some n, j : ℕ such that a = z(n, j).  Then

        a · 1 = z(n, j) · 1
              = z(n, j) · z(1, 0)
              = z(n, j)
              = a.
    
Theorem.  Let a, b : ℤ.  If a ≠ 0 and b ≠ 0, then a · b ≠ 0.

Proof.  There are some n, j, k, i : ℕ such that a = z(n, j) and b = z(k, i).  Assume that a ≠ 0 and b ≠ 0.  Then n ≠ j and k ≠ i.  Since k ≠ i, by [trichotomy] we have k < i or i < k.

Suppose that i < k.  Then k = i + s(u) for some u : ℕ.  Since n ≠ j, it follows by [cancellation law for multiplication] that n · s(u) ≠ j · s(u).  Hence

  n · k + j · i = n · (i + s(u)) + j · i
                   = n · i + n · s(u) + j · i
                   ≠ n · i + j · s(u) + j · i.

But

  n · i + j · s(u) + j · i = j · (i + s(u)) + n · i
                                 = j · k + n · i.

Thus n · k + j · i ≠ j · k + n · i.

This means that

 a · b = z(n · k + j · i, j · k + n · i) ≠ 0.
