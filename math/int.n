using nat;

# Integers: definition

Axiom.  There exists a type ℤ (the integers) with a function z : ℕ ⨯ ℕ → ℤ such that

  a. For all a, b, c, d ∈ ℕ, z(a, b) = z(c, d) if and only if a + d = b + c.  [equality on ℤ]
  b. For all x : ℤ, there exist some a, b : ℕ such that x = z(a, b).

Definition.  Let 0 : ℤ = z(0, 0).  Let 1 : ℤ = z(1, 0).

Lemma (ℤ zero).  For all a, b ∈ ℕ, z(a, b) = 0 if and only if a = b.

# Addition

Axiom.  There is a binary operation + : ℤ → ℤ → ℤ such that for all n, j, k, i : ℕ,

    z(n, j) + z(k, i) = z(n + k, j + i).

Axiom.  There is a unary operation − : ℤ → ℤ such that for all a, b ∈ ℕ,

  −z(a, b) = z(b, a).

Theorem.  Let a, b, c : ℤ.

  a. a + b = b + a.
  b. a + (b + c) = (a + b) + c.
  c. a + 0 = a.
  d. a + (−a) = 0.

Proof.

  a. There are some n, j, k, i : ℕ such that a = z(n, j) and b = z(k, i).  Then

    a + b = z(n, j) + z(k, i)
          = z(n + k, j + i)
          = z(k + n, i + j)
          = z(k, i) + z(n, j)
          = b + a.

  b. There are some n, j, k, i, m, h ∈ ℕ such that a = z(n, j) and b = z(k, i) and c = z(m, h).  Then
  
    (a + b) + c = (z(n, j) + z(k, i)) + z(m, h)
                    = z(n + k, j + i) + z(m, h)
                    = z((n + k) + m, (j + i) + h)
                    = z(n + (k + m), j + (i + h))
                    = z(n, j) + z(k + m, i + h)
                    = z(n, j) + (z(k, i) + z(m, h))
                    = a + (b + c).

  c. There are some n, j : ℕ such that a = z(n, j).  So

    a + 0 = z(n, j) + z(0, 0)
            = z(n + 0, j + 0)
            = z(n, j)
            = a.

  d. There are some n, j : ℕ such that a = z(n, j).  So

    a + (−a) = z(n, j) + −z(n, j)
                = z(n, j) + z(j, n)
                = z(n + j, j + n)
                = z(0, 0)     by [equality on ℤ]
                = 0.

# Integers: multiplication

Axiom.  There is a binary operation · : ℤ → ℤ → ℤ such that for all n, j, k, i : ℕ,

    z(n, j) · z(k, i) = z(n · k + j · i, j · k + n · i).  [def of ·]

Theorem.  Let a, b, c : ℤ.

  a. a · b = b · a.
  b. (a · b) · c = a · (b · c).
  c. a · (b + c) = a · b + a · c.  [ℤ left distributive]
  d. (a + b) · c = a · c + b · c.  [ℤ right distributive]
  e. a · 1 = a.

Proof.

  a. There are some n, j, k, i : ℕ such that a = z(n, j) and b = z(k, i).  Then

    a · b = z(n, j) · z(k, i)
            = z(n · k + j · i, j · k + n · i)
            = z(k · n + i · j, i · n + k · j)
            = z(k, i) · z(n, j)
            = b · a.

  b. There are some n, j, k, i, m, h : ℕ such that a = z(n, j) and b = z(k, i) and c = z(m, h).  Then

    (a · b) · c = (z(n, j) · z(k, i)) · z(m, h)
          = z(n · k + j · i, j · k + n · i) · z(m, h)
          = z((n · k + j · i) · m + (j · k + n · i) · h,
                 (j · k + n · i) · m + (n · k + j · i) · h)
          = z(((n · k) · m + (j · i) · m) + ((j · k) · h + (n · i) · h),
                 ((j · k) · m + (n · i) · m) + ((n · k) · h + (j · i) · h)) by [right distributive]
          = z(n · (k · m) + n · (i · h) + j · (i · m) + j · (k · h),
                 j · (k · m) + j · (i · h) + n · (i · m) + n · (k · h))
                  by [associativity of multiplication] and [commutativity of addition]
          = z(n · (k · m + i · h) + j · (i · m + k · h),
                 j · (k · m + i · h) + n · (i · m + k · h))  by [right distributive]
          = z(n, j) · z(k · m + i · h, i · m + k · h)
          = z(n, j) · (z(k, i) · z(m, h))
          = a · (b · c).

  c. There are some n, j, k, i, m, h : ℕ such that a = z(n, j) and b = z(k, i) and c = z(m, h).  Then

      a · (b + c) = z(n, j) · (z(k, i) + z(m, h))
                      = z(n, j) · z(k + m, i + h)
                      = z(n · (k + m) + j · (i + h), j · (k + m) + n · (i + h))
                      = z(n · k + n · m + j · i + j · h,
                          j · k + j · m + n · i + n · h)
                      = z(n · k + j · i,  j · k + n · i) +
                            z(n · m + j · h, j · m + n · h)
                      = z(n, j) · z(k, i) + z(n, j) · z(m, h) by [def of ·]
                      = a · b + a · c.

  d. (a + b) · c = c · (a + b) = c · a + c · b = a · c + b · c.

  e. There are some n, j : ℕ such that a = z(n, j).  Then

        a · 1 = z(n, j) · 1
              = z(n, j) · z(1, 0)
              = z(n, j)
              = a.
    
Theorem.  Let i, j : ℤ.  If i · j = 0, then i = 0 or j = 0.

Proof.

  Suppose that i · j = 0 and that i ≠ 0.  We will deduce that j = 0.  There are some a, b, c, d : ℕ such that i = z(a, b) and j = z(c, d).  Then a ≠ b by [ℤ zero].  By [def of ·] we see that 0 = i · j = z(a, b) · z(c, d) = z(a · c + b · d, b · c + a · d).  Then a · c + b · d = a · d + b · c by [ℤ zero].  By [trichotomy] we know that either a < b or a > b.
  
  First, suppose that a < b.  Then there is some g : ℕ such that a + s(g) = b.  Hence a · c + (a + s(g)) · d = a · d + (a + s(g)) · c.  So a · c + a · d + s(g) · d = a · d + a · c + s(g) · c, so s(g) · d = s(g) · c.  We deduce that d = c.

  Next, suppose that a > b.  Then there is some g : ℕ such that b + s(g) = a.  Hence (b + s(g)) · c + b · d = (b + s(g)) · d + b · c.  So b · c + s(g) · c + b · d = b · d + s(g) · d + b · c, so s(g) · c = s(g) · d.  We deduce that c = d.

  In either case c = d.  Then j = 0 by [ℤ zero].

# Theorems that hold in ℤ and any ring

Theorem.  Let x, y : ℤ.

    x + y = 0 if and only if y = −x.

Proof.  If y = −x, then x + y = 0.  Now suppose that x + y = 0.  Then

    y = 0 + y = (x + −x) + y = (x + y) + −x = 0 + −x = −x.

Theorem.  Let x, y, z : ℤ.

    a. If x + y = x + z then y = z.
    b. −0 = 0.
    c. −(−x) = x.
    d. If −x = −y, then x = y.
    e. −(x + y) = (−x) + (−y).
    f. If x + y = x, then y = 0.    [addend is 0]
    g. x · 0 = 0 = 0 · x.
    h. −(x · y) = (−x) · y = x · (−y).
    i. (−x) · (−y) = x · y.

Proof.

    a. Assume that x + y = x + z.  Then (−x) + x + y = (−x) + x + z.  Hence 0 + y = 0 + z.  Therefore y = z.
    
    c. (−x) + x = x + (−x) = 0.  It follows that −(−x) = x.

    g. x · 0 = x · (0 + 0) = x · 0 + x · 0, so x · 0 = 0 by [addend is 0].  Similarly, 0 · x = (0 + 0) · x = 0 · x + 0 · x, so 0 · x = 0 by [addend is 0].

    h. x · y + (−x) · y = (x + −x) · y = 0 · y = 0, so −(x · y) = (−x) · y. 
    
    Similarly, x · y + x · (−y) = x · (y + (−y)) = x · 0 = 0, so x · (−y) = −(x · y).

# Subtraction

Definition.  For all x, y : ℤ, x − y = x + (−y).

Theorem.  Let x, y, z : ℤ.

    a. x − x = 0.
    b. −(x − y) = y − x.
    c. x − y = 0 if and only if x = y.
    d. x · (y − z) = x · y − x · z.
    e. (y − z) · x = y · x − z · x.
