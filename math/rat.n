using int;

# Rational numbers: definition

Axiom.  There exists a type ℚ (the rational numbers) with a function q : ℤ ⨯ ℤ → ℚ such that

    a. Let a, b, c, d : ℤ.  Suppose that b ≠ 0 and d ≠ 0.  q(a, b) = q(c, d) if and only if ad = bc.
    b. For all r : ℚ there exist some a, b : ℤ such that r = q(a, b) and b ≠ 0.

Definition.  Let 0 : ℚ = q(0, 1).  Let 1 : ℚ = q(1, 1).

Lemma.  Let a, b: ℤ with b ≠ 0.

    a. q(a, b) = 0 if and only if a = 0.
    b. q(a, b) = 1 if and only if a = b.

# Addition

Axiom.  There is a binary operation + : ℚ → ℚ → ℚ such that for all a, b, c, d : ℤ with b ≠ 0 and d ≠ 0,

    q(a, b) + q(c, d) = q(ad + bc, bd).

Axiom.  There is a unary operation − : ℚ → ℚ such that for all a, b : ℤ with b ≠ 0,

    −q(a, b) = q(−a, b).

# Multiplication

Axiom.  There is a binary operation · : ℚ → ℚ → ℚ such that for all a, b, c, d : ℤ with b ≠ 0 and d ≠ 0,

    q(a, b) · q(c, d) = q(ac, bd).

# ℚ satisfies the axioms for an integral domain

Theorem.  Let r, s, t : ℚ.

    a. r + (s + t) = (r + s) + t.
    b. r + s = s + r.
    c. r + 0 = r.
    d. r + −r = 0.

Proof.

    a. There are some a, b, c, d, e, f : ℤ such that r = q(a, b) and s = q(c, d) and t = q(e, f) and b ≠ 0 and d ≠ 0 and f ≠ 0.

        (r + s) + t = q(ad + bc, bd) + q(e, f)
                         = q((ad + bc)f + bde, bdf).

    On the other hand,

        r + (s + t) = q(a, b) + q(cf + de, df)
                         = q(a(df) + b(cf + de), b(df)).

    But (ad + bc)f + bde = adf + b(cf + de).  Hence,

        (r + s) + t = r + (s + t).

    b. There are some a, b, c, d : ℤ such that r = q(a, b) and s = q(c, d) and b ≠ 0 and d ≠ 0.

        r + s = q(ad + bc, bd).
        s + r = q(cb + da, db).

    Hence, r + s = s + r.

    c. There are some a, b : ℤ such that r = q(a, b) and b ≠ 0.  Then r + 0 = q(a, b) + q(0, 1) = q(a · 1 + b · 0, b · 1) = q(a, b) = r.

    d. There are some a, b : ℤ such that r = q(a, b) and b ≠ 0. Then
    
        r + −r = q(a, b) + q(−a, b)
                   = q(ab + b(−a), b²)
                   = q(ab − ab, b²) = q(0, b²) = 0.
    