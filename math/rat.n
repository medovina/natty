using int;

# Rational numbers: definition

Axiom.  There exists a type ℚ (the rational numbers) with a function q : ℤ ⨯ ℤ → ℚ such that

    a. Let a, b, c, d : ℤ.  Suppose that b ≠ 0 and d ≠ 0.  q(a, b) = q(c, d) if and only if ad = bc.  [equality on ℚ]
    b. For all r : ℚ there exist some a, b : ℤ such that r = q(a, b) and b ≠ 0.

Definition.  Let 0 : ℚ = q(0, 1).  Let 1 : ℚ = q(1, 1).

Lemma.  Let a, b: ℤ with b ≠ 0.

    a. q(a, b) = 0 if and only if a = 0.  [0 if numerator is 0]
    b. q(a, b) = 1 if and only if a = b.  [1 if numerator = denominator]

# Addition

Axiom.  There is a binary operation + : ℚ → ℚ → ℚ such that for all a, b, c, d : ℤ with b ≠ 0 and d ≠ 0,

    q(a, b) + q(c, d) = q(ad + bc, bd).   [addition on ℚ]

Axiom.  There is a unary operation − : ℚ → ℚ such that for all a, b : ℤ with b ≠ 0,

    −q(a, b) = q(−a, b).

# Multiplication

Axiom.  There is a binary operation · : ℚ → ℚ → ℚ such that for all a, b, c, d : ℤ with b ≠ 0 and d ≠ 0,

    q(a, b) · q(c, d) = q(ac, bd).

# Here we show that ℚ is a commutative ring.

Theorem.  Let r, s, t : ℚ.

    a. r + (s + t) = (r + s) + t.
    b. r + s = s + r.
    c. r + 0 = r.
    d. r + −r = 0.
    e. r · (s · t) = (r · s) · t.
    f. r · 1 = r.
    g. r · s = s · r.
    h. r · (s + t) = r · s + r · t.
    i. (r + s) · t = r · t + s · t.

Proof.

    a. There are some a, b, c, d, e, f : ℤ such that r = q(a, b) and s = q(c, d) and t = q(e, f) and b ≠ 0 and d ≠ 0 and f ≠ 0.

        (r + s) + t = q(ad + bc, bd) + q(e, f)
                         = q((ad + bc)f + bde, bdf).

    On the other hand,

        r + (s + t) = q(a, b) + q(cf + de, df)
                         = q(a(df) + b(cf + de), b(df)).

    But (ad + bc)f + bde = adf + b(cf + de).  Hence,

        (r + s) + t = r + (s + t).

    b. There are some a, b, c, d : ℤ such that r = q(a, b) and s = q(c, d) and b ≠ 0 and d ≠ 0.

        r + s = q(ad + bc, bd).
        s + r = q(cb + da, db).

    Hence, r + s = s + r.

    c. There are some a, b : ℤ such that r = q(a, b) and b ≠ 0.  Then r + 0 = q(a, b) + q(0, 1) = q(a · 1 + b · 0, b · 1) = q(a, b) = r.

    d. There are some a, b : ℤ such that r = q(a, b) and b ≠ 0. Then
    
        r + −r = q(a, b) + q(−a, b)
                   = q(ab + b(−a), b²)
                   = q(ab − ab, b²) = q(0, b²) = 0.
    
    e. There are some a, b, c, d, e, f : ℤ such that r = q(a, b) and s = q(c, d) and t = q(e, f) and b ≠ 0 and d ≠ 0 and f ≠ 0.

        r · (s · t) = q(a, b) · q(ce, df)
                      = q(a(ce), b(df)).

        (r · s) · t = q(ac, bd) · q(e, f) = q((ac)e, (bd)f).

    Hence r · (s · t) = (r · s) · t.

    f. There are some a, b : ℤ such that r = q(a, b) and b ≠ 0. Then

        r · 1 = q(a, b) · q(1, 1) = q(a · 1, b · 1)
                = q(a, b) = r.

    g. There are some a, b, c, d : ℤ such that r = q(a, b) and s = q(c, d) and b ≠ 0 and d ≠ 0.

        r · s = q(ac, bd).
        s · r = q(ca, db) = q(ac, bd).

    h. There are some a, b, c, d, e, f : ℤ such that r = q(a, b) and s = q(c, d) and t = q(e, f) and b ≠ 0 and d ≠ 0 and f ≠ 0.

        r · (s + t) = q(a, b) · q(cf + de, df)
                       = q(a(cf + de), b(df))
                       = q(acf + ade, b(df)).

        r · s + r · t = q(a, b) · q(c, d) + q(a, b) · q(e, f)
                         = q(ac, bd) + q(ae, bf)
                         = q(acbf + aebd, b²df) by [addition on ℚ]
                         = q(b(acf + ade), b²df).

    But

        q(acf + ade, b(df)) = q(b(acf + ade), b²df) by [equality on ℚ].

# Because we have shown that ℚ is a commutative ring, all the theorems we proved about ℤ using only ring axioms should now be applicable to ℚ.  Unfortunately Natty cannot yet recognize this correspondence, so for now we reprove some of those theorems here.

Theorem.  Let x, y, z : ℚ.

    a. If x + y = x + z then y = z.  [add cancel left on ℚ]
    b. If x + y = x, then y = 0.    [addend is 0 on ℚ]
    c. x · 0 = 0 = 0 · x.  [multiply by 0 on ℚ]

Proof.

    a. Assume that x + y = x + z.  Then −x + (x + y) = −x + (x + z), so (−x + x) + y = (−x + x) + z, so 0 + y = 0 + z.  Therefore y = z.

    b. If x + y = x then x + y = x + 0, so by [add cancel left on ℤ] y = 0.

    c. x · 0 = x · (0 + 0) = x · 0 + x · 0, so x · 0 = 0 by [addend is 0 on ℚ].  Similarly, 0 · x = (0 + 0) · x = 0 · x + 0 · x, so 0 · x = 0 by [addend is 0 on ℚ].

# Now we show that ℚ is an integral domain.

Theorem.  Let r, s : ℚ.  Suppose that r ≠ 0 and s ≠ 0.  Then r · s ≠ 0.

Proof.  There are some a, b, c, d : ℤ such that r = q(a, b) and s = q(c, d) and b ≠ 0 and d ≠ 0.  Now

    r · s = q(a, b) · q(c, d) = q(ac, bd).

Since r ≠ 0 and r = q(a, b), it follows by [0 if numerator is 0] that a ≠ 0.  Likewise, since s ≠ 0 and s = q(c, d) we know that c ≠ 0.  Since a ≠ 0 and c ≠ 0 by [zero product on ℤ] we conclude that ac ≠ 0.  Hence q(ac, bd) ≠ 0, so r · s = q(ac, bd) ≠ 0.

# We now show that ℚ is a field.

Theorem.  0: ℚ ≠ 1.

Theorem.  Let r : ℚ.  Suppose that r ≠ 0.  Then there exists s : ℚ such that r · s = 1.

Proof.  There are some a, b : ℤ such that r = q(a, b) and b ≠ 0.  By [0 if numerator is 0] a ≠ 0.  Therefore

    r · q(b, a) = q(a, b) · q(b, a) = q(ab, ba) = 1 by [1 if numerator = denominator].

# Inverses are unique

Theorem (unique inverses).  Let x, y, z : ℚ.  Suppose that x · y = 1 and x · z = 1.  Then y = z.

Proof.

    y = 1 · y = (x · z) · y = (z · x) · y
       = z · (x · y) = z · 1 = z.

Axiom.  There exists a binary operation ^ : ℚ → ℤ → ℚ such that for all x : ℚ, if x ≠ 0 then

    x · x⁻¹ = 1.

Lemma.  Let x, y : ℚ.  Suppose that x · y = 1.  Then x = y⁻¹.

Proof.  If y = 0 then x · y = 0, which is a contradiction.  So y ≠ 0.  Then y · y⁻¹ = 1.  By [unique inverses] x = y⁻¹. 

Axiom.  There exists a binary operation / :  ℚ → ℚ → ℚ such that for all x, y : ℚ, if y ≠ 0 then

    x / y = x · y⁻¹.  [definition of /]

# Properties of fields

Theorem.  Let u, v, x, y, z: ℚ.

    a. (1 : ℚ)⁻¹ = 1.
    b. If x ≠ 0 then x⁻¹ ≠ 0.
    c. If x ≠ 0 then (x⁻¹)⁻¹ = x.
    d. If x ≠ 0 then x⁻¹ = 1 / x.
    e. If x ≠ 0 and y ≠ 0 then (x · y)⁻¹ = x⁻¹ · y⁻¹.
    f. x / 1 = x.
    g. If x ≠ 0 then 0 / x = 0.
    h. If x ≠ 0 then x / x = 1.
    i. Suppose that y ≠ 0.  Then z · y = x if and only if z = x / y.
    j. If y ≠ 0 and v ≠ 0 then (x / y) · (u / v) = (x · u) / (y · v).
    k. If y ≠ 0 and v ≠ 0 then x / y = (x · v) / (y · v).
    l. If y ≠ 0 and v ≠ 0 then x / y + u / v = (x · v + y · u) / (y · v).

Proof.

    a. (1 : ℚ) · 1 = 1.  Hence 1 = (1 : ℚ)⁻¹.

    f. By [definition of /] x / 1 = x · 1⁻¹.  x · 1⁻¹ = x · 1 = x.

    i. Suppose that z · y = x.  Then
    
        z = z · 1 = z · (y · y⁻¹) = (z · y) · y⁻¹ = x · y⁻¹ = x / y.

    Now suppose that z = x / y.  Then

        z · y = (x / y) · y = (x · y⁻¹) · y = x · (y · y⁻¹) = x · 1 = x.

    j. Suppose that y ≠ 0 and v ≠ 0.  Then

        (x / y) · (u / v) = (x · y⁻¹) · (u · v⁻¹)
                              = (x · u) · (y⁻¹ · v⁻¹)
                              = (x · u) · (y · v)⁻¹
                              = (x · u) / (y · v).
    
    l. Suppose that y ≠ 0 and v ≠ 0.  Then

        (x · v + y · u) / (y · v) = (x · v + y · u)(y · v)⁻¹
                                          = (x · v + y · u)(y⁻¹ · v⁻¹)
                                          = x · v · y⁻¹ · v ⁻¹ + y · u · y⁻¹ · v⁻¹
                                          = x · y⁻¹ · (v · v⁻¹) + u · v⁻¹ · (y · y⁻¹)
                                          = x · y⁻¹ · 1 + u · v⁻¹ · 1
                                          = x · y⁻¹ + u · v⁻¹ = x / y + u / v.
