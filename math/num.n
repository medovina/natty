using int;

Definition.  For all a, b : ℤ, a | b iff there exists c : ℤ such that b = ac.

Theorem.  Let a, b, c : ℤ.

    a. a | 0.
    b. 1 | a.
    c. a | a.
    d. If a | b and b | c then a | c.
    e. If c ≠ 0 and a | c then |a| ≤ |c|.  [divides implies ≤]
    f. If 0 | c then c = 0.  [0 divides only 0]
    g. a | b and b | a if and only if a = b or a = −b.
    h. a | a · b.

Proof.

    d. Suppose that a | b and b | c.  Then there are some d, e : ℤ such that b = ad and c = be.  Then c = (ad)e = a(de).  Hence a | c.

    e. Suppose that a | c and c ≠ 0.  Then there exists some b : ℤ such that c = ab.  Hence |c| = |a| · |b|.  c ≠ 0, so b ≠ 0.  We know that |b| ≥ 0, so we have |b| > 0.  Therefore by [< iff successor ≤ on ℤ] |b| ≥ 0 + 1 = 1 : ℤ.  Hence
    
        |c| = |a| · |b|
             ≥ |a| · 1  by [≤ mul right on ℤ]
             = |a|.

    f. Suppose that 0 | c.  Then c = 0 · d for some d : ℤ, so c = 0.

    g. Suppose that a | b and b | a.  If a = 0 or b = 0 then by [0 divides only 0] a = 0 = b.  If a ≠ 0 and b ≠ 0 then by [divides implies ≤] |a| ≤ |b|, also by [divides implies ≤] |b| ≤ |a|, so by [trichotomy on ℤ] |a| = |b|, so a = b or a = −b.

    Now suppose that a = b.  Then clearly a | b and b | a.  Finally, suppose that a = −b.  Then a = −(1 · b) = −1 · b.  Also b = −a = −(1 · a) = −1 · a, so a | b and b | a.
