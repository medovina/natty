# Natural numbers: definition

Axiom.  There exists a type ℕ with an element 0 ∈ ℕ and a function s : ℕ → ℕ such that

  a. There is no n ∈ ℕ such that s(n) = 0.  [nat_succ_zero]
  b. For all n, m ∈ ℕ, if s(n) = s(m) then n = m.  [nat_succ_injective]
  c. Let P : ℕ → 𝔹.  If 0 ∈ P, and k ∈ P implies s(k) ∈ P for all k ∈ ℕ, then P = ℕ.  [nat_induction]

Definition.  Let 1 : ℕ = s(0).  Let 2 : ℕ = s(1).

Lemma (nat_prec).  Let a ∈ ℕ.  Suppose that a ≠ 0.  Then there is some b ∈ ℕ such that a = s(b).

# Natural numbers: addition

Axiom.  There is a binary operation + : ℕ → ℕ → ℕ such that for all n, m ∈ ℕ,

  a. n + 0 = n.
  b. n + s(m) = s(n + m).

Theorem.  Let a, b, c ∈ ℕ.

  1. If a + c = b + c, then a = b.      [nat_add_cancel]
  2. (a + b) + c = a + (b + c).
  3. 0 + a = a = a + 0.
  4. 1 + a = s(a) = a + 1.
  5. s(a) + b = s(a + b).
  6. a + b = b + a.
  7. a ≠ s(a). 
  8. s(a) + s(b) ≠ 1.       [nat_no_sum_one]
  9. a + s(b) ≠ a.
  10. If a + b = 0, then a = 0 and b = 0.

# Natural numbers: multiplication

Axiom.  There is a binary operation · : ℕ → ℕ → ℕ such that for all n, m ∈ ℕ,

  a. n · 0 = 0.
  b. n · s(m) = (n · m) + n.

Theorem.  Let a, b, c ∈ ℕ.

  1. a · 0 = 0 = 0 · a.
  2. a · 1 = a = 1 · a.  [nat_mul_ident]
  3. c(a + b) = ca + cb.
  4. (ab)c = a(bc). 
  5. s(a) · b = ab + b.  [nat_succ_mul]
  6. ab = ba.
  7. (a + b)c = ac + bc.

Theorem.  Let a, b ∈ ℕ.

  1. ab = 0 if and only if a = 0 or b = 0.      [nat_prod_0]
  2. ab = 1 if and only if a = 1 and b = 1.   [nat_prod_1]

Proof.

  2. Let a, b ∈ ℕ.  If a = 1 and b = 1, then ab = 1.
  
  Now suppose that ab = 1.  If a = 0 then ab = 0, a contradiction.  Similarly, if b = 0 then ab = 0, a contradiction.  So a ≠ 0 and b ≠ 0.  Then by :nat_prec there exist c, d ∈ ℕ such that a = s(c) and b = s(d).

  Suppose that c ≠ 0.  Then there exists e ∈ ℕ such that c = s(e), so a = s(s(e)).  Then

    s(0) = 1 = s(s(e)) · s(d)
                   = s(e) · s(d) + s(d)
                   = e · s(d) + s(d) + s(d)
                   = s(e · s(d) + d) + s(d)
                   = s(e · s(d) + d + s(d))
                   = s(s(e · s(d) + d + d)).

  By :nat_succ_injective, 0 = s(e · s(d) + d + d).  This is a contradiction to :nat_succ_zero.  So it must be that c = 0.  Therefore a = s(0) = 1.  Then 1 · b = 1.  By :nat_mul_ident 1 · b = b, so we have b = 1.  And so a = b = 1.

# Right cancellation of multiplication

Theorem.  Let a, b, c ∈ ℕ.  If c ≠ 0 and ac = bc then a = b.

Proof. Let

        G = { x ∈ ℕ | for all y, z ∈ ℕ, if z ≠ 0 and xz = yz then x = y }.

  Let b, c ∈ ℕ with c ≠ 0 and 0 · c = bc. Then bc = 0.  Since c ≠ 0, we must have b = 0 by :nat_prod_0.  So 0 = b, and hence 0 ∈ G.

  Now let a ∈ ℕ, and suppose that a ∈ G.  Let b, c ∈ ℕ, and suppose that c ≠ 0 and s(a) · c = bc.  Then by :nat_succ_mul we deduce that ca + c = bc.  If b = 0, then either s(a) = 0 or c = 0, which is a contradiction.  Hence b ≠ 0.  By :nat_prec there is some p ∈ ℕ such that b = s(p).  Therefore ca + c = s(p) · c, and we see that ca + c = cp + c.  It follows by :nat_add_cancel that ca = cp, so ac = pc.  By hypothesis it follows that a = p.  Therefore s(a) = s(p) = b.  Hence s(a) ∈ G, and we deduce that G = ℕ.

# Natural numbers: inequalities

Definition.  For all a, b ∈ ℕ, a ≤ b iff there is some c ∈ ℕ such that a + c = b.  a < b iff a ≤ b and a ≠ b.

Definition.  For all a, b ∈ ℕ, we write a ≥ b iff b ≤ a.  We write a > b iff b < a.

Theorem.  Let a, b, c ∈ ℕ.

  1. a ≤ a, and a ≮ a, and a < s(a).   [nat_lt]
  2. 0 ≤ a.  [nat_ge_0]
  3. If a ≤ b and b ≤ c, then a ≤ c.   [nat_le_trans]
  4. If a ≤ b and b < c, then a < c.
  5. If a < b and b ≤ c, then a < c.
  6. If a < b and b < c, then a < c.   [nat_lt_trans]

Proof.

  3. Let a, b, c ∈ ℕ.  Suppose that a ≤ b and b ≤ c.  Then there exist d, e ∈ ℕ such that a + d = b and b + e = c.  Then a + (d + e) = (a + d) + e = b + e = c, so a ≤ c.

  4. Let a, b, c ∈ ℕ.  Suppose that a ≤ b and b < c.  Then b ≤ c, so a ≤ c by part (3) of this theorem.  There exist d, e ∈ ℕ such that a + d = b and b + e = c.  So a + (d + e) = (a + d) + e = b + e = c.  If e = 0 then b = c, which is a contradiction.  So e ≠ 0, and so there exists f ∈ ℕ such that e = s(f).  Then a + (d + s(f)) = c, so a + s(d + f) = c.  If a = c then a + s(d + f) = a, which is a contradiction.  So a ≠ c.  It follows that a < c.

  5. Let a, b, c ∈ ℕ.  Suppose that a < b and b ≤ c.  Then a ≤ b, so a ≤ c by part (3) of this theorem.  There exist d, e ∈ ℕ such that a + d = b and b + e = c.  So a + (d + e) = (a + d) + e = b + e = c.  If d = 0 then a = b, which is a contradiction.  So d ≠ 0, and so there exists f ∈ ℕ such that d = s(f).  Then a + (s(f) + e) = c, so a + s(f + e) = c.  If a = c then a + s(f + e) = a, which is a contradiction.  So a ≠ c.  It follows that a < c.

Theorem.  Let a, b ∈ ℕ.

  1. Precisely one of a < b or a = b or a > b holds.        [nat_trichotomy]
  2. a ≤ b if and only if a ≯ b.
  3. a ≤ b or b ≤ a.
  4. If a ≤ b and b ≤ a, then a = b.         [nat_le_antisymm]

Proof.

  1. Let a, b ∈ ℕ.  Suppose that a < b and a = b.  It then follows that a < a, which is a contradiction to :nat_lt.  Now suppose that a = b and a > b.  It follows that a > a, which is similarly a contradiction.  Now suppose that a < b and b < a.  By :nat_lt_trans we deduce that a < a, again a contradiction.

  Now let

      G = { x ∈ ℕ | for all y ∈ ℕ, x < y or x = y or x > y }.

  We will show that x ∈ G for all x ∈ ℕ.  We start by showing that 0 ∈ G.  Let y ∈ ℕ.  By :nat_ge_0 we know that 0 ≤ y.  It follows that y = 0 or y > 0.  Hence 0 ∈ G.

  Now let x ∈ ℕ, and suppose that x ∈ G.  We will show that s(x) ∈ G.  Let y ∈ ℕ.  By hypothesis we know that x < y or x = y or x > y.
  
  First suppose that x < y.  Then there is some p ∈ ℕ such that x + p = y.  If p = 0, then x = y, so s(x) > y by :nat_lt.  If p ≠ 0, then by :nat_prec there is some r ∈ ℕ such that p = s(r), which implies that x + s(r) = y, which implies s(x) + r = y, so s(x) ≤ y, so either s(x) < y or s(x) = y.

  Next suppose that x = y.  Then by :nat_lt it follows that s(x) > x = y.  Finally, suppose that x > y.  We know that s(x) > x, and by :nat_lt_trans it follows that s(x) > y.

  Putting the cases together, we see that s(x) < y or s(x) = y or s(x) > y always holds.  Hence s(x) ∈ G, and we conclude that G = ℕ.

  3. Let a, b ∈ ℕ.  We know that a < b or a = b or a > b.  If a < b, then a ≤ b.  If a = b, then a ≤ b.  If a > b, then b ≤ a.  In any case a ≤ b or b ≤ a.

# More identities involving inequalities

Theorem.  Let a, b, c ∈ ℕ.

  1. a < b if and only if s(a) < s(b).
  2. If a < b then a + c < b + c.           [nat_lt_add_right]
  3. If a + c < b + c then a < b.           [nat_lt_add_cancel]
  4. If c ≠ 0 and a < b, then ac < bc.
  5. If ac < bc, then a < b.
  
Proof.

  1. Let a, b ∈ ℕ.  Suppose that a < b.  Then there is some c ∈ ℕ such that a + c = b.  So a + 1 + c = b + 1.  Then s(a) + c = s(b), so s(a) < s(b).

  Now suppose that s(a) < s(b).  Then there is some c ∈ ℕ such that s(a) + c = s(b).  So a + 1 + c = b + 1.  Then a + c = b, so a < b.

  2. Let a, b ∈ ℕ.  Suppose that a < b.  Let

      G = { c ∈ ℕ | a + c < b + c }.

  Clearly 0 ∈ G.  Let c ∈ ℕ, and suppose that c ∈ G.  Then a + c < b + c, so s(a + c) < s(b + c) by part (1) of this theorem.  Then a + s(c) < b + s(c), so s(c) ∈ G.  Hence G = ℕ.

  3. Let a, b, c ∈ ℕ, and suppose that a + c < b + c.  If a = b then b + c < b + c, a contradiction. If b < a then b + c < a + c by part (2) of this theorem, so b + c < a + c < b + c, also a contradiction.  By :nat_trichotomy, the only alternative is a < b.

  4. Let a, b ∈ ℕ, and suppose that a < b.  Let

    G = { c ∈ ℕ | if c ≠ 0, then ac < bc }.

  Clearly 0 ∈ G.  Let c ∈ ℕ, and suppose that c ∈ G.  We will show that s(c) ∈ G.  We know that a · 1 < b · 1.  Suppose that c = 0. Then a · s(c) < b · s(c), so s(c) ∈ G.  Conversely, suppose that c ≠ 0.  By hypothesis we know that ac < bc.  Then by :nat_lt_add_right we have ac + a < bc + a, and also bc + a < bc + b.  So ac + a < bc + b, so a · s(c) < b · s(c), so s(c) ∈ G.  And so G = ℕ.

  5. Let a, b, c ∈ ℕ, and suppose that ac < bc.  If c = 0 then 0 < 0, which is a contradiction.  So c ≠ 0.  If a = b then bc < bc, a contradiction.  If b < a then bc < ac by part (4) of this theorem, so bc < ac < bc, also a contradiction.  By :nat_trichotomy, the only alternative is a < b.

Theorem.  Let a, b ∈ ℕ.

  1. It cannot be that b < a < b + 1.   [nat_discrete]
  2. a ≤ b if and only if a < b + 1.       [nat_discrete_le]
  3. a < b if and only if a + 1 ≤ b.

Proof.

  1. Let a, b ∈ ℕ.  Suppose that b < a < b + 1.  Then there are g, h ∈ ℕ such that b + g = a and a + h = b + 1.  Then (b + g) + h = b + 1, so (g + h) + b = 1 + b, so g + h = 1.  If g = 0 then b = a, which is a contradiction.  So there is some j ∈ ℕ such that g = s(j).  Similarly, if h = 0 then a = b + 1, which is a contradiction.  So there is some k ∈ ℕ such that h = s(k).  Therefore s(j) + s(k) = 1.  This is a contradiction to :nat_no_sum_one.

  2. Let a, b ∈ ℕ.  First suppose that a ≤ b.  Suppose further that a ≥ b + 1.  Then by :nat_le_trans we deduce that b + 1 ≤ b, which is a contradiction to :nat_lt.  So it must be that a < b + 1.  Second, suppose that a < b + 1.  Suppose further that a > b.  It follows that b < a < b + 1, which is a contradiction to part (1) of this theorem.  So it must be that a ≤ b.

  3. Let a, b ∈ ℕ.  First suppose that a < b.  Suppose further that a + 1 > b.  It follows that a < b < a + 1, which is a contradiction to part (1) of this theorem.  So it must be that a + 1 ≤ b. Second, suppose that a + 1 ≤ b.  Suppose further that a ≥ b.  Then by :nat_le_trans we deduce that a + 1 ≤ a, which is a contradiction to :nat_lt.  So it must be that a < b.

Lemma (nat_add_prod).  Let a, b, c, d ∈ ℕ.  If a < b and c < d, then bc + ad < ac + bd.

Proof.

  Let a, b, c, d ∈ ℕ, and suppose that a < b and c < d.  Then there exist g, h ∈ ℕ with g ≠ 0 and h ≠ 0 such that b = a + g and d = c + h.  Then

    ac + bd = ac + (a + g)(c + h)
                = ac + a(c + h) + g(c + h)
                = ac + ac + ah + g(c + h)
                = ac + ac + ah + gc + gh
                = ac + gc + ac + ah + gh
                = (a + g)c + a(c + h) + gh
                = (bc + ad) + gh.

   We must have gh ≠ 0, and so bc + ad < ac + bd.

# Well-ordering principle on naturals

Definition.  For all S : ℕ → 𝔹, S is empty iff there is no n : ℕ such that n ∈ S.  S is nonempty iff S is not empty.

Definition.  For all A : ℕ → 𝔹 and a : ℕ, a is a least element of A iff a ∈ A and for all x : ℕ, if x ∈ A then a ≤ x.

Theorem (nat_well_ordered). Let G : ℕ → 𝔹.  Suppose that G is nonempty.  Then there exists some m : ℕ such that m is a least element of G.

Proof.  Let G : ℕ → 𝔹, and suppose that G is nonempty.  Suppose that there is no m : ℕ such that m ∈ G and for all g ∈ ℕ, if g ∈ G then m ≤ g.  Let

    H = { a ∈ ℕ | for all n ∈ ℕ, if n ≤ a then n ∉ G }.

We will show that H = ℕ.

Suppose that 0 ∉ H.  Then there is some q ∈ ℕ such that q ≤ 0 and q ∈ G.  By :nat_le_antisymm we see that q = 0.  Hence 0 ∈ G.  It then follows from :nat_ge_0 that for all g ∈ ℕ, if g ∈ G then 0 ≤ g, which is a contradiction.  Therefore 0 ∈ H.

Now let a : ℕ, and suppose that a ∈ H.  Suppose further that a + 1 ∉ H.  Then there is some p ∈ ℕ such that p ≤ a + 1 and p ∈ G.  If p ≤ a, then we have p ∉ G, which is a contradiction.  Hence by :nat_trichotomy we deduce that a < p.  Therefore a < p ≤ a + 1, and it follows from :nat_discrete that p = a + 1.  Hence a + 1 ∈ G.  Now let x : ℕ, and suppose that x ∈ G.  Suppose that x < a + 1.  Then x ≤ a by :nat_discrete_le.  Because a ∈ H, it follows that x ∉ G, which is a contradiction.  Hence a + 1 ≤ x by :nat_trichotomy.  So for all g : ℕ, if g ∈ G then a + 1 ≤ g.  This is a contradiction.  It follows that a + 1 ∈ H, and hence H = ℕ.

Let b : ℕ.  We know that b ∈ H. Since b ≤ b, we have b ∉ G.  And so G is empty, which is a contradiction.

# Integers: definition

Axiom.  There exists a type ℤ with a function z : ℕ ⨯ ℕ → ℤ such that

  a. For all a, b, c, d ∈ ℕ, z(a, b) = z(c, d) if and only if a + d = b + c.  [int_eq_def]
  b. For all x ∈ ℤ, there exist some a, b ∈ ℕ such that x = z(a, b).

Definition.  Let 0 : ℤ = z(0, 0).  Let 1 : ℤ = z(1, 0).  Let 2 : ℤ = z(2, 0).

Lemma (int_z_zero).  For all a, b ∈ ℕ, z(a, b) = 0 if and only if a = b.

Lemma.  0 : ℤ ≠ 1 : ℤ.

# Integers: addition

Axiom (int_ax_add).  There is a binary operation + : ℤ → ℤ → ℤ such that for all a, b, c, d ∈ ℕ,

    z(a, b) + z(c, d) = z(a + c, b + d).

Axiom.  There is a unary operation − : ℤ → ℤ such that for all a, b ∈ ℕ,

  −z(a, b) = z(b, a).

Definition.  For all i, j ∈ ℤ, i − j = i + (−j).

Theorem.  Let i, j, k ∈ ℤ.

  1. (i + j) + k = i + (j + k).
  2. i + j = j + i.
  3. i + 0 = i.
  4. i + (−i) = 0.

Proof.

  1. Let i, j, k ∈ ℤ. We know that there are some a, b, c, d, e, f ∈ ℕ such that i = z(a, b) and j = z(c, d) and k = z(e, f).  Then
  
    (i + j) + k = (z(a, b) + z(c, d)) + z(e, f)
                    = z(a + c, b + d) + z(e, f)
                    = z((a + c) + e, (b + d) + f)
                    = z(a + (c + e), b + (d + f))
                    = z(a, b) + z(c + e, d + f)
                    = z(a, b) + (z(c, d) + z(e, f))
                    = i + (j + k).

  2. Let i, j ∈ ℤ.  There are some a, b, c, d ∈ ℕ such that i = z(a, b) and j = z(c, d).  Then

    i + j = z(a, b) + z(c, d)
          = z(a + c, b + d)
          = z(c + a, d + b)
          = z(c, d) + z(a, b)
          = j + i.

  3. Let i ∈ ℤ.  Then there are some a, b ∈ ℕ such that i = z(a, b).  So

    i + 0 = z(a, b) + z(0, 0)
            = z(a + 0, b + 0)
            = z(a, b)
            = i.

  4. Let i ∈ ℤ.  Then there are some a, b ∈ ℕ such that i = z(a, b).  So

    i + (−i) = z(a, b) + −z(a, b)
                = z(a, b) + z(b, a)
                = z(a + b, b + a)
                = z(0, 0)     by :int_eq_def
                = 0.

Theorem.  Let x, y, z ∈ ℤ.

  1. If x + z = y + z, then x = y.
  2. −(−x) = x.
  3. −(x + y) = (−x) + (−y).

Proof.

  1. Let x, y, z ∈ ℤ, and suppose that x + z = y + z.  Then (x + z) + (−z) = (y + z) + (−z), so x + (z + −z) = y + (z + −z), so x + 0 = y + 0, so x = y.

  2. Let x ∈ ℤ.  Then there are some a, b ∈ ℕ such that x = z(a, b).  Then

    −(−x) = −(− z(a, b))
            = − z(b, a)
            = z(a, b)
            = x.

  3. Let x, y ∈ ℤ.  Then there are some a, b, c, d ∈ ℕ such that x = z(a, b) and y = z(c, d).  Then

    −(x + y) = −(z(a, b) + z(c, d))
                = −(z(a + c, b + d))
                = z(b + d, a + c)
                = z(b, a) + z(d, c)
                = −z(a, b) + −z(c, d)
                = −x + −y.

# Integers: multiplication

Axiom.  There is a binary operation · : ℤ → ℤ → ℤ such that for all a, b, c, d ∈ ℕ,

    z(a, b) · z(c, d) = z(ac + bd, ad + bc).        [int_mul_def]

Theorem.  Let i, j, k ∈ ℤ.

  1. (ij)k = i(jk).
  2. ij = ji.
  3. i · 0 = 0.
  4. i · 1 = i.
  5. i(j + k) = ij + ik.
  6. (i + j)k = ik + jk.
  
Proof.

  1. Let i, j, k ∈ ℤ.  There are some a, b, c, d, e, f ∈ ℕ such that i = z(a, b) and j = z(c, d) and k = z(e, f).  Then

    (ij)k = (z(a, b) · z(c, d)) · z(e, f)
          = z(ac + bd, ad + bc) · z(e, f)
          = z((ac + bd)e + (ad + bc)f, (ac + bd)f + (ad + bc)e)
          = z(ace + bde + adf + bcf, acf + bdf + ade + bce)
          = z(ace + adf + bcf + bde, acf + ade + bce + bdf)
          = z(a(ce + df) + b(cf + de), a(cf + de) + b(ce + df))
          = z(a, b) · z(ce + df, cf + de)
          = z(a, b) · (z(c, d) · z(e, f))
          = i(jk).

  2. Let i, j ∈ ℤ.  Then there are some a, b, c, d ∈ ℕ such that i = z(a, b) and j = z(c, d).  Then

    ij = z(a, b) · z(c, d)
      = z(ac + bd, ad + bc)
      = z(ca + db, cb + da)
      = z(c, d) · z(a, b)
      = ji.

  3. Let i ∈ ℤ.  Then there are some a, b ∈ ℕ such that i = z(a, b).  Then

    i · 0 = z(a, b) · 0
          = z(a, b) · z(0, 0)
          = z(0, 0)
          = 0.

  4. Let i ∈ ℤ.  Then there are some a, b ∈ ℕ such that i = z(a, b).  Then

    i · 1 = z(a, b) · 1
          = z(a, b) · z(1, 0)
          = z(a, b)
          = i.

  5. Let i, j, k ∈ ℤ.  There are some a, b, c, d, e, f ∈ ℕ such that i = z(a, b) and j = z(c, d) and k = z(e, f).  Then

    i(j + k) = z(a, b) · (z(c, d) + z(e, f))
                = z(a, b) · z(c + e, d + f)
                = z(a(c + e) + b(d + f), a(d + f) + b(c + e))
                = z(ac + ae + bd + bf, ad + af + bc + be)
                = z(ac + bd + ae + bf, ad + bc + af + be)
                = z(ac + bd, ad + bc) + z(ae + bf, af + be)
                = z(a, b) · z(c, d) + z(a, b) · z(e, f)
                = ij + ik.

  6. Let i, j, k ∈ ℤ.  Then (i + j)k = k(i + j) = ki + kj = ik + jk.

Theorem (int_mul_zero).  Let i, j ∈ ℤ.  If ij = 0, then i = 0 or j = 0.

Proof.

  Let i, j ∈ ℤ.  Suppose that ij = 0 and that i ≠ 0.  We will deduce that j = 0.  There are some a, b, c, d ∈ ℕ such that i = z(a, b) and j = z(c, d).  Then a ≠ b by :int_z_zero.  By :int_mul_def we see that 0 = ij = z(a, b) · z(c, d) = z(ac + bd, ad + bc).  Then ac + bd = ad + bc by :int_z_zero.  By :nat_trichotomy we know that either a < b or a > b.
  
  First, suppose that a < b.  Then there is some g ∈ ℕ such that a + g = b.  Hence ac + (a + g)d = ad + (a + g)c.  So ac + ad + gd = ad + ac + gc, so gd = gc.  Since a ≠ b, we must have g ≠ 0, and we deduce that d = c.

  Next, suppose that a > b.  Then there is some g ∈ ℕ such that b + g = a.  Hence (b + g)c + bd = (b + g)d + bc.  So bc + gc + bd = bd + gd + bc, so gc = gd.  Since a ≠ b, we must have g ≠ 0, and we deduce that c = d.  

  In either case c = d.  Then j = 0 by :int_z_zero.

Theorem.  Let i, j, k ∈ ℤ.

  1. If k ≠ 0 and ik = jk, then i = j.
  2. (−i)j = −(ij).
  3. i(−j) = −(ij).

Proof.

  1. Let i, j, k ∈ ℤ, and suppose that k ≠ 0 and ik = jk.  Then ik + (−j)k = jk + (−j)k, so (i + −j)k = (j + −j)k = 0 · k = 0.  By :int_mul_zero we have i + −j = 0.  Then i + −j + j = 0 + j, so i = j.

  2. Let i, j, k ∈ ℤ.  Then
  
    i(−j) = i(−j) + (ij + −(ij))
          = (i(−j) + ij) + −(ij)
          = i(−j + j) + −(ij)
          = i · 0 + −(ij)
          = −(ij).

# Integers: inequalities

Axiom.  There is a relation < : ℤ → ℤ → 𝔹 such that for all a, b, c, d ∈ ℕ,

    z(a, b) < z(c, d) if and only if a + d < b + c.     [int_lt_def]

Definition.  For all i, j ∈ ℤ, i ≤ j iff i < j or i = j.

Corollary.  For all a, b, c, d ∈ ℕ,

    z(a, b) ≤ z(c, d) if and only if a + d ≤ b + c.     [int_le]

Definition.  For all i, j ∈ ℤ, we write i > j iff j < i.  We write i ≥ j iff j ≤ i.

Theorem.  Let i, j, k ∈ ℤ.

  1. Precisely one of i < j or i = j or i > j holds.    [int_trichotomy]
  2. If i < j and j < k, then i < k.        [int_lt_trans]
  3. If i ≤ j and j < k, then i < k.
  4. If i < j and j ≤ k, then i < k.        [int_lt_le_trans]
  5. If i ≤ j and j ≤ k, then i ≤ k.        [int_le_trans]
  6. If i < j then i + k < j + k.            [int_lt_add_right]
  7. If i < j and k > 0, then ik < jk.    [int_lt_mul_right]

Proof.

  1. Let i, j ∈ ℤ.  Then there are some a, b, c, d ∈ ℕ such that i = z(a, b) and j = z(c, d).  By :int_trichotomy we know that exactly one of a + d < b + c or a + d = b + c or a + d > b + c holds.  The result follows using :int_lt_def.

  2. Let i, j, k ∈ ℤ, and suppose that i < j and j < k.  There are some a, b, c, d, e, f ∈ ℕ such that i = z(a, b) and j = z(c, d) and k = z(e, f).  Then z(a, b) < z(c, d) and z(c, d) < z(e, f), so a + d < b + c and c + f < d + e.  It follows that a + d + f < b + c + f and b + c + f < b + d + e, so a + d + f < b + d + e.  Then a + f < b + e by :nat_lt_add_cancel, so i < k.

  6. Let i, j, k ∈ ℤ, and suppose that i < j.  There are some a, b, c, d, e, f ∈ ℕ such that i = z(a, b) and j = z(c, d) and k = z(e, f).  Then z(a, b) < z(c, d), and therefore a + d < b + c.  Hence (a + d) + (e + f) < (b + c) + (e + f), and therefore (a + e) + (d + f) < (b + f) + (c + e).  It follows by :int_lt_def that z(a + e, b + f) < z(c + e, d + f), and hence z(a, b) + z(e, f) < z(c, d) + z(e, f), which means that i + k < j + k.

  7. Let i, j, k ∈ ℤ, and suppose that i < j and k > 0.  There are some a, b, c, d, e, f ∈ ℕ such that i = z(a, b) and j = z(c, d) and k = z(e, f). So z(a, b) < z(c, d) and z(e, f) > 0, which means that a + d < b + c and f < e.  So by :nat_add_prod we have
  
    (b + c)f + (a + d)e < (a + d)f + (b + c)e.

  Then
  
    (ae + bf) + (cf + de) < (af + be) + (ce + df).
    
  So z(ae + bf, af + be) < z(ce + df, cf + de) by :int_lt_def.  And so z(a, b) · z(e, f) < z(c, d) · z(e, f), so we have ik < jk.

Theorem.  Let i, j ∈ ℤ.

  1. i > 0 if and only if −i < 0, and i < 0 if and only if −i > 0.  [int_lt_neg]
  2. 0 : ℤ < 1 : ℤ.
  3. If i ≤ j and j ≤ i, then i = j.       [int_le_antisymm]
  4. If i > 0 and j > 0, then ij > 0.
  5. If i > 0 and j < 0, then ij < 0.
  6. If i > 0 and j ≤ 0, then ij ≤ 0.   [int_mul_pos_nonneg]
  7. If i < 0 and j < 0, then ij > 0.   [int_mul_neg_neg]
  8. If i ≤ 0 and j ≤ 0, then ij ≥ 0.

# Bijection between naturals and non-negative integers

Axiom (int_nat_proj). There is a function π : ℤ → ℕ such that for all i ∈ ℤ, if i ≥ 0 then i = z(π(i), 0).

Theorem.  Let i, j ∈ ℤ.

  1. If i ≥ 0 and j ≥ 0 and π(i) = π(j), then i = j.    [int_proj_injective]
  2. If i ≥ 0 and j ≥ 0, then π(i + j) = π(i) + π(j).
  3. If i ≥ 0 and j ≥ 0, then i ≤ j iff π(i) ≤ π(j).       [int_proj_lt]

Proof.

    1. Let i, j ∈ ℤ, and suppose that i ≥ 0 and j ≥ 0 and π(i) = π(j).  Suppose that i ≠ j.  Then z(π(i), 0) ≠ z(π(j), 0), so π(i) + 0 ≠ π(j) + 0 by :int_eq_def, so π(i) ≠ π(j), which is a contradiction.

    2. Let i, j ∈ ℤ, and suppose that i ≥ 0 and j ≥ 0.  Then
    
        z(π(i + j), 0) = i + j                            by :int_nat_proj
                            = z(π(i), 0) + z(π(j), 0)   by :int_nat_proj
                            = z(π(i) + π(j), 0)           by :int_ax_add  .
    
    Then by :int_eq_def we have π(i + j) + 0 = π(i) + π(j) + 0, so π(i + j) = π(i) + π(j).

    3. Let i, j ∈ ℤ, and suppose that i ≥ 0 and j ≥ 0.  Suppose that π(i) ≤ π(j).  Then π(i) + 0 ≤ 0 + π(j), so z(π(i), 0) ≤ z(π(j), 0) by :int_le.  Then i ≤ j by :int_nat_proj.  Conversely, suppose that i ≤ j.  Then z(π(i), 0) ≤ z(π(j), 0) by :int_nat_proj.  Then π(i) + 0 ≤ 0 + π(j) by :int_le, so π(i) ≤ π(j).

# Mathematical induction on non-negative integers

Theorem (int_induction).  For all S : ℤ → 𝔹, if 0 ∈ S and for all i ∈ ℤ i ≥ 0 and i ∈ S implies i + 1 ∈ S, then for all i ∈ ℤ i ≥ 0 implies i ∈ S.

Proof.  Let S : ℤ → 𝔹, and suppose that 0 ∈ S and for all i ∈ ℤ i ≥ 0 and i ∈ S implies i + 1 ∈ S.  Let T = { n ∈ ℕ | there exists i ∈ ℤ such that i ≥ 0 and i ∈ S and n = π(i) }.  0 = π(0), so 0 ∈ T.  Now let n : ℕ, and suppose that n ∈ T.  Then there exists some i ∈ ℤ such that i ≥ 0 and i ∈ S and n = π(i). So i + 1 ∈ S.  Now, π(i + 1) = π(i) + π(1) = n + 1 = s(n), so s(n) ∈ T.  We have shown that n ∈ T implies s(n) ∈ T for all n ∈ ℕ.  Then T = ℕ by :nat_induction.  Now let i ∈ ℤ, and suppose that i ≥ 0.  Since T = ℕ, there exists j ∈ ℤ such that j ≥ 0 and j ∈ S and π(i) = π(j).  Then by :int_proj_injective we have i = j, so i ∈ S.

# Well-ordering principle on non-negative integers

Definition.  For all S : ℤ → 𝔹, S is empty iff there is no n : ℤ such that n ∈ S.  S is nonempty iff S is not empty.

Definition.  For all A : ℤ → 𝔹 and a : ℤ, a is a least element of A iff a ∈ A and for all x : ℤ, if x ∈ A then a ≤ x.  a is a greatest element of A iff a ∈ A and for all x : ℤ, if x ∈ A then x ≤ a.

Theorem (int_greatest_unique).  For all A : ℤ → 𝔹 and x, y : ℤ, if x is a greatest element of A, and y is a greatest element of A, then x = y.

Proof.  Let A : ℤ → 𝔹.  Let x, y : ℤ.  Suppose that x is a greatest element of A, and y is a greatest element of A.  Then x ≤ y and y ≤ x.  It follows by :int_le_antisymm that x = y.

Theorem (int_well_ordered). Let S : ℤ → 𝔹.  Suppose that S is nonempty, and for all i ∈ ℤ, if i ∈ S then i ≥ 0.  Then there exists some m : ℤ such that m is a least element of S.

Proof.  Let S : ℤ → 𝔹, and suppose that S is nonempty and for all i ∈ ℤ, if i ∈ S then i ≥ 0.  Then there exists some j : ℤ such that j ∈ S and j ≥ 0.  Let T = { n ∈ ℕ | there exists i ∈ ℤ such that i ≥ 0 and i ∈ S and n = π(i) }.  By :nat_well_ordered there is some a : ℕ such that a is a least element of T.  Since a ∈ T, there is some m : ℤ such that m ≥ 0 and m ∈ S and a = π(m).  We will show that m is a least element of S.  Let x : ℤ, and suppose that x ∈ S.  Then x ≥ 0.  Then π(x) ∈ T.  Since a is a least element of T, we know that a ≤ π(x).  So π(m) ≤ π(x), and then m ≤ x by :int_proj_lt.

# Discreteness of integers

Theorem.  Let x, y ∈ ℤ.

    1. It cannot be that x < y < x + 1.     [int_discrete]
    2. x ≤ y if and only if x < y + 1.         [int_discrete_le]
    3. x < y if and only if x + 1 ≤ y.         [int_discrete_lt]

Proof.

    1. Let x, y ∈ ℤ.  Suppose that x < y < x + 1.  Then x − x < y − x < (x + 1) − x, and we deduce that 0 < y − x < 1.  By :int_lt_trans we know that y − x ≥ 0, and also 1 : ℤ ≥ 0.  Then by :int_proj_lt π(0) < π(y − x) < π(1), which is a contradiction to :nat_discrete.

    2. Let x, y ∈ ℤ.  First suppose that x ≤ y.  Suppose further that x ≥ y + 1.  Then by :int_le_trans we deduce that y + 1 ≤ y.  However 1 : ℤ > 0, so by :int_lt_add_right we know that y + 1 > y + 0 = y.  This is a contradiction.  Second, suppose that x < y + 1.  Suppose further that x > y.  It follows that y < x < y + 1, which is a contradiction to part (1) of this theorem.  So it must be that x ≤ y.

    3. Let x, y ∈ ℤ.  First suppose that x < y.  Suppose further that x + 1 > y.  Then x < y < x + 1, which is a contradiction to part (1) of this theorem.  So it must be that x + 1 ≤ y.  Second, suppose that x + 1 ≤ y.  Then by :int_le_trans we deduce that x + 1 ≤ x.  However 1 : ℤ > 0, so by :int_lt_add_right we know that x + 1 > x + 0 = x.  This is a contradiction.

Theorem.  Let a, b, c ∈ ℤ.  If c > 0 and ab = c, then a ≤ c.    [int_factor_less]

Proof.  Let a, b, c ∈ ℤ, and suppose that c > 0 and ab = c.  If a < 0 then by :int_lt_trans we have a < c.  If a = 0 then c = 0, which is a contradiction.  Now suppose that a > 0.  If b ≤ 0 then by :int_mul_pos_nonneg we have c ≤ 0, a contradiction.  So b > 0.  By :int_discrete_lt b ≥ 1.  Then by :int_lt_mul_right we have b · a ≥ 1 · a, so c ≥ a.

# Functions

Definition. For all f : ℤ → ℤ and S, T : ℤ → 𝔹, f is a mapping from S to T iff for all x : ℤ, if x ∈ S then f(x) ∈ T. 

Definition. For all f : ℤ → ℤ and S, T : ℤ → 𝔹, f is an injection from S to T iff f is a mapping from S to T and for all x, y : ℤ, if x ∈ S and y ∈ S and f(x) = f(y) then x = y.

Definition. For all f : ℤ → ℤ and S, T : ℤ → 𝔹, f is a surjection from S to T iff f is a mapping from S to T and for all y : ℤ, if y ∈ T then there exists some x : ℤ such that x ∈ S and f(x) = y.

Definition. For all f : ℤ → ℤ and S, T : ℤ → 𝔹, f is a bijection from S to T iff f is an injection from S to T and f is a surjection from S to T.

# Finite and infinite sets

Definition.  For all S, T : ℤ → 𝔹, S ~ T iff there exists some f : ℤ → ℤ such that f is a bijection from S to T.

Definition.  For all S : ℤ → 𝔹, S is finite iff there exists n : ℤ such that S ~ { x ∈ ℤ | x < n }.   S is infinite iff S is not finite.

# Divisibility

Definition.  For all a, b ∈ ℤ, a | b iff there is some c ∈ ℤ such that b = ac.

Lemma.  For all a ∈ ℤ, a | a.

Proof.  Let a ∈ ℤ.  a = a · 1, so a | a.

Lemma (divisor_le).  For all a, b ∈ ℤ, if b > 0 and a | b then a ≤ b.

Proof.  Let a, b ∈ ℤ, and suppose that b > 0 and a | b.  Then there is some c ∈ ℤ such that b = ac.  By :int_factor_less, a ≤ b.

Theorem (int_div_trans). For all a, b, c ∈ ℤ, if a | b and b | c then a | c.

Proof.  Let a, b, c : ℤ, and suppose that a | b and b | c.  There is some e ∈ ℤ such that ae = b.  Similarly, there is some f ∈ ℤ such that bf = c.  Hence c = bf = (ae)f = a(ef), and we conclude that a | c.

Theorem (int_divides_linear_combo).  For all a, b, m, n, c ∈ ℤ, if c | a and c | b then c | (ma + nb).

Proof.  Let a, b, m, n, c : ℤ, and suppose that c | a and c | b.  There is some e ∈ ℤ such that a = ce.  Similarly, there is some f ∈ ℤ such that b = cf.  Hence ma + nb = mce + ncf = c(me + nf).  We see that c | (ma + nb).

Theorem (int_division_algorithm).  For all a, b ∈ ℤ, if b > 0 then there exist q, r ∈ ℤ such that a = bq + r and 0 ≤ r < b.

Proof.  Let a, b ∈ ℤ, and suppose that b > 0.  Let S = { i ∈ ℤ | i ≥ 0 and there exists k ∈ ℤ such that i = a − bk }.  We will show that S is nonempty.  Suppose that a ≥ 0.  Then a − b · 0 ≥ 0, so a − b · 0 ∈ S.  Conversely, suppose that a < 0.  There exists d : ℤ such that d = a − b · a.  Then d = a(1 − b).  b > 0, so by :int_discrete b ≥ 1.  Then b − b ≥ 1 − b, so 1 − b ≤ 0.  It follows by :int_mul_neg_neg that d ≥ 0, so d ∈ S.  In any case S is nonempty.

By :int_well_ordered, there exists some r : ℤ such that r is a least element of S.  r ∈ S, so r ≥ 0.  We know that there exists q : ℤ such that r = a − bq.  Suppose that r ≥ b.  There exists d : ℤ such that d = a − b(q + 1).  Then d = a − bq − b = r − b.  Since r ≥ b, we have r − b ≥ 0, so d ≥ 0, so d ∈ S.  By :int_lt_neg −b < 0, so r > r − b by :int_lt_add_right, so d < r.  This is a contradiction.  Hence 0 ≤ r < b and a = bq + r.

# Prime numbers

Definition.  For all a ∈ ℤ, a is prime iff a > 1 and for all b ∈ ℤ, if b > 0 and b | a then b = 1 or b = a.

Lemma.  For all p ∈ ℤ, if p is prime then p ∤ 1.

Proof.  Let p : ℤ, and suppose that p is prime.  Suppose that p | 1.  Then p ≤ 1 by :divisor_le, however p > 1 by definition.  This is a contradiction, so p ∤ 1.

Lemma (int_has_prime_divisor).  For all a ∈ ℤ, if a > 1 then there exists p ∈ ℤ such that p is prime and p | a.

Proof.  Suppose that there exists some k ∈ ℤ such that k > 1 and there exists no p ∈ ℤ such that p is prime and p | k.  Let

    G = { a ∈ ℤ | a > 1 and there exists no p ∈ ℤ such that p is prime and p | a }.

We have k ∈ G.  So by :int_well_ordered there exists some n : ℤ such that n ∈ G and for all g ∈ ℤ, if g ∈ G then n ≤ g.  There exists no p ∈ ℤ such that p is prime and p | n, but n | n, so n is not prime.  Hence there exist a, b ∈ ℤ such that n = ab and 1 < a < n and 1 < b < n.  Because a < n, there must exist some p ∈ ℤ such that p is prime and p | a.  By :int_div_trans, for all d ∈ ℤ, if d | a then d | n.  So we must have p | n, which is a contradiction.

# Natty parses the product pᵢ ⋯ pⱼ into ∏(p, i, j), which we define here.
Axiom. There is a function ∏ : (ℤ → ℤ) ⨯ ℤ ⨯ ℤ → ℤ such that for all p : ℤ → ℤ,

 a. For all i, j : ℤ, if j < i then ∏(p, i, j) = 1.
 b. For all i, j : ℤ, if j ≥ i then ∏(p, i, j) = p(i) · ∏(p, i + 1, j).

# Euclid's theorem, Wiedjik #11
Theorem (euclid).  { x ∈ ℤ | x is prime } is infinite.

Proof.  Suppose that there exists n : ℤ and p : ℤ → ℤ with n > 0 such that for all x : ℤ, x is prime iff x = pᵢ for some i : ℤ with 1 ≤ i ≤ n.  Let

           Q = p₁ ⋯ pₙ + 1.
    
By :int_has_prime_divisor, there exists q : ℤ such that q is prime and q | Q.  Suppose that there is some j : ℤ such that 1 ≤ j ≤ n and q = pⱼ.  We know that Q − p₁ ⋯ pₙ = 1. q | Q and q | p₁ ⋯ pₙ, so by :int_divides_linear_combo it follows that q | 1.  This is a contradiction.  So q is prime, but there is no j : ℤ such that 1 ≤ j ≤ n and q = pⱼ.  This is a contradiction, so { x ∈ ℤ | x is prime } is infinite.

# Greatest common divisors

Definition.  For all a, b, c ∈ ℤ, c is a common divisor of a and b iff c | a and c | b.

Axiom.  There is a function gcd : ℤ ⨯ ℤ → ℤ such that for all a, b ∈ ℤ, if a ≠ 0 or b ≠ 0 then gcd(a, b) is a greatest element of { x ∈ ℤ | x is a common divisor of a and b }.

Theorem (gcd_zero).  For all a ∈ ℤ, if a > 0 then gcd(a, 0) = a.

Proof.  Let a ∈ ℤ, and suppose that a > 0.  a | a and a | 0, so gcd(a, 0) ≥ a.  For all b ∈ ℤ, if b | a then b ≤ a by :divisor_le.  So gcd(a, 0) ≤ a.  It follows by :int_le_antisymm that gcd(a, 0) = a.

Theorem (gcd_symmetric).  For all a, b ∈ ℤ, if a ≠ 0 or b ≠ 0 then gcd(a, b) = gcd(b, a).

Proof.  Let a, b ∈ ℤ, and suppose that a ≠ 0 or b ≠ 0.  Let S = { x ∈ ℤ | x is a common divisor of a and b }.  Let T = { x ∈ ℤ | x is a common divisor of b and a }.  gcd(a, b) is a greatest element of S, and gcd(b, a) is a greatest element of T.  But for all x, y, z ∈ ℤ, x is a common divisor of y and z iff x is a common divisor of z and y.  So S = T, so gcd(b, a) is a greatest element of S.  Then by :int_greatest_unique we have gcd(a, b) = gcd(b, a).

Theorem (gcd_add_multiple).  For all a, b, c : ℤ, if a ≠ 0 or b ≠ 0 then gcd(a + cb, b) = gcd(a, b).

Proof.  Let a, b, c : ℤ, and suppose that a ≠ 0 and b ≠ 0.  We will show that for all x : ℤ, x is a common divisor of a and b iff x is a common divisor of a + cb and b.  Let e : ℤ, and suppose that e is a common divisor of a and b.  By :int_divides_linear_combo, we see that e | (a + cb), so e is a common divisor of a + cb and b.  Now let f : ℤ, and suppose that f is a common divisor of a + cb and b.  Then by :int_divides_linear_combo we see that f | ((a + cb) − cb), so f | a.  So f is a common divisor of a and b.  So { x ∈ ℤ | x is a common divisor of a + cb and b } = { x ∈ ℤ | x is a common divisor of a and b }.  Hence gcd(a + cb, b) = gcd(a, b).

Definition.  For all a, b, c : ℤ, c is a linear combination of a and b iff there exist m, n : ℤ such that c = ma + nb.

Lemma (gcd_lemma).  For all d, a, b ∈ ℤ, if d is a least element of { i ∈ ℤ | i > 0 and i is a linear combination of a and b }, then d | a.

Proof.  Let d, a, b ∈ ℤ.  Suppose that d is a least element of { i ∈ ℤ | i > 0 and i is a linear combination of a and b }.

There exist m, n ∈ ℤ such that

    d = ma + nb.

By :int_division_algorithm there exist q, r ∈ ℤ such that

    a = dq + r and 0 ≤ r < d.

We see that

    r = a − dq = a − q(ma +nb) = (1 − qm)a − qnb.

This shows that r is a linear combination of a and b.  Because 0 ≤ r < d, and d is a least element of { i ∈ ℤ | i > 0 and i is a linear combination of a and b }, we conclude that r = 0.  Hence d | a.

Theorem.  For all a, b ∈ ℤ, if a ≠ 0 or b ≠ 0 then gcd(a, b) is a least element of { i ∈ ℤ | i > 0 and i is a linear combination of a and b }.

Proof.  Let a, b ∈ ℤ, and suppose that a ≠ 0 or b ≠ 0. Let S = { i ∈ ℤ | i > 0 and i is a linear combination of a and b }.  If a > 0 then 1 · a + 0 · b ∈ S.  If a < 0 then (−1)a + 0 · b ∈ S.  So S is nonempty, so by :int_well_ordered there exists some d ∈ ℤ such that d is a least element of S.  So there exist m, n ∈ ℤ such that

    d = ma + nb.

By :gcd_lemma d | a and d | b.

We will show that d = gcd(a, b).  Let c : ℤ, and suppose that c | a and c | d.  d is a linear combination of a and b, so by :int_divides_linear_combo we know that c | d.  Then by :divisor_le c ≤ d.  So d = gcd(a, b).

# Bezout's Theorem, Wiedjik #60
Corollary (bezout).  For all a, b ∈ ℤ, if a ≠ 0 or b ≠ 0 then there exist m, n ∈ ℤ such that ma + nb = gcd(a, b).

# Euclid's algorithm

Axiom.  There exists a function r : ℤ ⨯ ℤ ⨯ ℤ → ℤ such that for all a, b ∈ ℤ,

    a. r(a, b, 0) = a.
    b. r(a, b, 1) = b.
    c. For all i : ℤ, if i ≥ 0 and r(a, b, i + 1) > 0 then r(a, b, i) = r(a, b, i + 1) · q + r(a, b, i + 2) for some q ∈ ℤ and 0 ≤ r(a, b, i + 2) < r(a, b, i + 1).  [euclidean_sequence]

Lemma (decrease_terminates).  For all f : ℤ → ℤ, if f(0) > 0 and for all i ∈ ℤ i ≥ 0 and f(i) > 0 implies that 0 ≤ f(i + 1) < f(i), then there exists some i ∈ ℤ such that i ≥ 0 and f(i) = 0.

Proof.  Let f : ℤ → ℤ, and suppose that f(0) > 0 and for all i ∈ ℤ, i ≥ 0 and f(i) > 0 implies that 0 ≤ f(i + 1) < f(i).  Assume that for all i ∈ ℤ, if i ≥ 0 then f(i) ≠ 0.  Let S = { i ∈ ℤ | 0 < f(i) ≤ f(0) − i }.  We have 0 < f(0) ≤ f(0) − 0, so 0 ∈ S.  Now let i ∈ ℤ, and suppose that i ≥ 0 and i ∈ S.  Then 0 < f(i) ≤ f(0) − i, and also 0 ≤ f(i + 1) < f(i).  By assumption f(i + 1) ≠ 0, so f(i + 1) > 0.  Also f(i + 1) < f(i) ≤ f(0) − i, so by :int_lt_le_trans f(i + 1) < f(0) − i, so f(i + 1) < f(0) − (i + 1) + 1, so by :int_discrete_le we have f(i + 1) ≤ f(0) − (i + 1).  This shows that i + 1 ∈ S.  Then by :int_induction we know that for all i ∈ ℤ, i ≥ 0 implies i ∈ S.  But then f(0) ∈ S, so 0 < f(0) ≤ f(0) − f(0), which implies that 0 : ℤ < 0, a contradiction.  So there must exist some i ∈ ℤ such that i ≥ 0 and f(i) = 0.

Lemma (euclid_terminates).  For all a, b ∈ ℤ, if a ≥ b > 0 then there exists some i ∈ ℤ such that i ≥ 0 and r(a, b, i) = 0.

Proof.  Let a, b ∈ ℤ, and suppose that a ≥ b > 0. There exists f : ℤ → ℤ such that for all i ∈ ℤ, f(i) = r(a, b, i + 1).  f(0) = r(a, b, 1) = b, so f(0) > 0.  Let i ∈ ℤ, and suppose that i ≥ 0 and f(i) > 0. Then r(a, b, i + 1) > 0, so 0 ≤ r(a, b, i + 2) < r(a, b, i + 1) by :euclidean_sequence, so 0 ≤ f(i + 1) < f(i).  Then by :decrease_terminates there is some j ∈ ℤ such that j ≥ 0 and r(a, b, j + 1) = 0.  There is some i ∈ ℤ such that i = j + 1.  We have i ≥ 0 and r(a, b, i) = 0.

# Euclidean algorithm, Wiedjik #69
Theorem.  For all a, b ∈ ℤ, if a ≥ b > 0 then there exists some j ∈ ℤ such that j is a least element of { i ∈ ℤ | i ≥ 0 and r(a, b, i) = 0 }, and r(a, b, j − 1) = gcd(a, b).

Proof.  Let a, b ∈ ℤ, and suppose that a ≥ b > 0.  Let S = { i ∈ ℤ | i ≥ 0 and r(a, b, i) = 0 }.  By :euclid_terminates we know that S is nonempty.  Then by :int_well_ordered there is some j ∈ ℤ such that j is a least element of S.  r(a, b, 0) = a > 0, so it must be that j > 0.

Let T = { i ∈ ℤ | if 0 ≤ i < j then gcd(r(a, b, i), r(a, b, i + 1)) = gcd(a, b) and r(a, b, i + 1) ≥ 0 }.  We know that r(a, b, 0) = a and r(a, b, 1) = b > 0, so 0 ∈ T.

Now let i ∈ ℤ, and suppose that i ≥ 0 and i ∈ T.  If i + 1 ≥ j then trivially i + 1 ∈ T.  Suppose that i + 1 < j.  By hypothesis r(a, b, i + 1) ≥ 0.  Since j is a least element of S we know that r(a, b, i + 1) ≠ 0, so r(a, b, i + 1) > 0.  Then by :euclidean_sequence we know that r(a, b, i) = r(a, b, i + 1) · q + r(a, b, i + 2) for some q ∈ ℤ, and r(a, b, i + 2) ≥ 0.  Now

    gcd(r(a, b, i + 1), r(a, b, i + 2))
        = gcd(r(a, b, i + 2), r(a, b, i + 1))  by :gcd_symmetric
        = gcd(r(a, b, i), r(a, b, i + 1))         by :gcd_add_multiple
        = gcd(a, b)                                     by hypothesis .

So i + 1 ∈ T.  Then by :int_induction we know that for all i ∈ ℤ, i ≥ 0 implies i ∈ T.

j > 0, so j − 1 ≥ 0 by :int_discrete_lt.  Then j − 1 ∈ T, so gcd(r(a, b, j − 1), r(a, b, j)) = gcd(a, b).  But j ∈ S, so r(a, b, j) = 0.  Now we have gcd(a, b) = gcd(r(a, b, j − 1), 0) = r(a, b, j − 1) by :gcd_zero.

Lemma (divides_other_factor).  For all a, b, c ∈ ℤ, if a > 0 and b > 0 and c > 0 and gcd(a, b) = 1 and a | bc, then a | c.

Proof.  Let a, b, c ∈ ℤ, and suppose that a > 0 and b > 0 and c > 0 and gcd(a, b) = 1 and a | bc.  Then by :bezout there exist x, y : ℤ such that ax + by = 1.  So acx + bcy = c.  acx + bcy is a linear combination of a and bc, and a | a and a | bc.  So by :int_divides_linear_combo a | acx + bcy.  Hence a | c.

Lemma.  For all p, n ∈ ℤ and a ∈ ℤ → ℤ, if p is prime and n ≥ 0 and p | a₁ ⋯ aₙ, then there exists i ∈ ℤ such that 1 ≤ i ≤ n and p | aᵢ.

Proof.  Let p ∈ ℤ, and suppose that p is prime.  Let a ∈ ℤ → ℤ.  Let

    S = { n ∈ ℤ | if p | a₁ ⋯ aₙ, then there exists i ∈ ℤ such that 1 ≤ i ≤ n and p | aᵢ }.

Trivially 1 ∈ S.  Now let n ∈ ℤ, and suppose that n ≥ 1 and n ∈ S.  Suppose that p | a₁ ⋯ aₙ₊₁.  We know that either gcd(p, a₁ ⋯ aₙ) = 1 or gcd(p, a₁ ⋯ aₙ) = p.  If gcd(p, a₁ ⋯ aₙ) = 1, then by :divides_other_factor p | aₙ₊₁.  On the other hand, if gcd(p, a₁ ⋯ aₙ) = p, then p | a₁ ⋯ aₙ, so by the induction hypothesis there exists i ∈ ℤ such that 0 ≤ i ≤ n and p | aᵢ.  In either case there exists some i ∈ ℤ such that 1 ≤ i ≤ n + 1 and p | aᵢ.  And so n + 1 ∈ S.  Then by :int_induction for all i ∈ ℤ if i ≥ 0 then i ∈ S.
