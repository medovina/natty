(* types *)

id_type = id ':' typ

ids_type = id {',' id}+ typ

(* terms *)

term = sym | id '(' expr ')' | var | '(' expr ')'

terms = term+

(* expressions *)

op = '·' | '+' | '=' | '≠'

expr = terms | expr op expr

atomic = expr ['is true']

(* small propositions *)

prop_op 'and' | 'implies' | ', and'

small_prop = atomic | small_prop prop_op small_prop | small_prop 'for all' id_type

(* propositions *)

if_then_prop = 'if' small_prop 'then' small_prop

for_all_prop = 'For all' ids_type ',' proposition

exists_prop =
    'There is' ('some' | 'none') id_type 'such that' proposition

proposition = for_all_prop | exists_prop | if_then_prop | small_prop

(* top propositions *)

let_prop = 'Let' id_type '.' top_prop

suppose = 'Suppose that' proposition {', and that' proposition} '.'
    'Then' proposition

top_prop = let_prop | suppose | proposition

(* proposition lists *)

label = (letter | {digit}) '.'

proposition_item = label top_prop '.' ['(' word ')']

prop_items = proposition_item+

top_prop_or_items = prop_items | top_prop '.'

propositions = ['for all' ids_type ','] top_prop_or_items

(* axioms *)

axiom_decl = 'a type' id |
    (('an element' | 'a function' | 'a binary operation') id_or_sym ':' typ)

axiom_group = 'Axiom.'
    ('There exists' | 'There is') axiom_decl {('and' | 'with') axiom_decl}
    'such that' propositions

(* definitions *)

definition = 'Definition. Let' sym ':' typ '=' term '.'

(* theorems *)

proof_item = label 'By' word 'on' var

proofs = 'Proof.' proof_item+

theorem_group = ('Lemma.' | 'Theorem.') 'Let' ids_type '.'
    top_prop_or_items [proofs]

(* program *)

program = {axiom_group | definition | theorem_group}
